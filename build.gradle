plugins {
    id "de.undercouch.download" version "3.4.3"
}

project.ext {
    if (System.getenv("UNITY_ROOT") != null) {
        unityRoot = System.getenv("UNITY_ROOT")
    } else {
        throw new GradleException("Set UNITY_ROOT env variable")
    }

    if (System.getenv("UNITY_USERNAME") != null) {
        unityUsername = System.getenv("UNITY_USERNAME")
    } else {
        throw new GradleException("Set UNITY_USERNAME env variable")
    }

    if (System.getenv("UNITY_PASSWORD") != null) {
        unityPassword = System.getenv("UNITY_PASSWORD")
    } else {
        throw new GradleException("Set UNITY_PASSWORD env variable")
    }

    pilgrimiOSVersion = "2.4.0"
    pilgrimAndroidVersion = "2.4.2"
    tempDir = file("temp").absolutePath
    pluginSource = file("source/pilgrim-unity-sdk").absolutePath
    pluginBuildDir = file("temp/pilgrim-unity-sdk").absolutePath
    exportPath = file("pilgrim-unity-sdk.unitypackage").absolutePath
}

configurations {
    pilgrim {
        repositories {
            mavenCentral()
            google()
            maven {
                url 'https://foursquare.jfrog.io/foursquare/libs-release/'
            }
        }
        
        exclude group: 'com.android.support', module: 'animated-vector-drawable'
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'asynclayoutinflater'
        exclude group: 'com.android.support', module: 'collections'
        exclude group: 'com.android.support', module: 'coordinatorlayout'
        exclude group: 'com.android.support', module: 'cursoradapter'
        exclude group: 'com.android.support', module: 'customview'
        exclude group: 'com.android.support', module: 'documentfile'
        exclude group: 'com.android.support', module: 'drawerlayout'
        exclude group: 'com.android.support', module: 'interpolator'
        exclude group: 'com.android.support', module: 'loader'
        exclude group: 'com.android.support', module: 'localbroadcastmanager'
        exclude group: 'com.android.support', module: 'print'
        exclude group: 'com.android.support', module: 'recyclerview-v7'
        exclude group: 'com.android.support', module: 'slidingpanelayout'
        exclude group: 'com.android.support', module: 'support-annotations'
        exclude group: 'com.android.support', module: 'support-compat'
        exclude group: 'com.android.support', module: 'support-core-ui'
        exclude group: 'com.android.support', module: 'support-core-utils'
        exclude group: 'com.android.support', module: 'support-fragment'
        exclude group: 'com.android.support', module: 'support-vector-drawable'
        exclude group: 'com.android.support', module: 'swiperefreshlayout'
        exclude group: 'com.android.support', module: 'transition'
        exclude group: 'com.android.support', module: 'versionedparcelable'
        exclude group: 'com.android.support', module: 'viewpager'
        
        dependencies {
            pilgrim "androidx.activity:activity:1.2.0-alpha06"
            pilgrim "com.foursquare:pilgrimsdk:${pilgrimAndroidVersion}"
            pilgrim "com.foursquare:pilgrimsdk-debugging:${pilgrimAndroidVersion}"
        }

        resolvedConfiguration.resolvedArtifacts.each { artifact ->
            copy {
                from artifact.file
                into "${tempDir}/android-deps"
                rename { "${artifact.id.componentIdentifier.group}-${artifact.name}-${artifact.id.componentIdentifier.version}.${artifact.extension}" }
            }
        }
    }
}

task copyPilgrimAndroid(type: Copy) {
    from("${tempDir}/android-deps")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/Android")
}

task buildAndCleanAndroidWrapperLibrary(type: GradleBuild) {
    buildFile = "source/android-source/build.gradle"
    tasks = ["clean", "assembleRelease"]
}

task copyAndroidWrapperLibrary(type: Copy) {
    from("source/android-source/pilgrimunitysdk/build/outputs/aar")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/Android")
    include("pilgrimunitysdk-release.aar")
    rename("pilgrimunitysdk-release.aar", "pilgrimunitysdk.aar")
}

copyAndroidWrapperLibrary.dependsOn(buildAndCleanAndroidWrapperLibrary)

task downloadPilgrimiOS(type: Download) {
    src "https://foursquare.jfrog.io/foursquare/pilgrimsdk-ios/v${pilgrimiOSVersion}/Pilgrim.framework.zip"
    dest "${tempDir}/Pilgrim.framework.zip"
}

task unzipPilgrimiOS() {
    doLast {
        exec {
            executable "unzip"
            args "${tempDir}/Pilgrim.framework.zip", "-d", "${tempDir}"
        }
    }
}

task copyPilgrimiOS(type: Copy) {
    from("${tempDir}/Pilgrim-${pilgrimiOSVersion}/Pilgrim.framework")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/iOS/Pilgrim.framework")
    include("**")
}

copyPilgrimiOS.dependsOn(downloadPilgrimiOS, unzipPilgrimiOS)

task loadLicenseFile() {
    doLast {
        exec {
            executable "${unityRoot}/Unity.app/Contents/MacOS/Unity"
            args "-batchmode",
                 "-manualLicenseFile", 
                 "/Library/Application Support/Unity/Unity_lic.ulf",
                 "-logFile", "unity-loadLicense.log",
                 "-quit"
            ignoreExitValue true
        }
    }
}

task exportUnityPackage() {
    doLast {
        exec {
            executable "${unityRoot}/Unity.app/Contents/MacOS/Unity"
            args "-gvh_disable",
                 "-batchmode",
                 "-nographics",
                 "-username",
                 "${unityUsername}",
                 "-password",
                 "${unityPassword}",
                 "-projectPath", "${pluginBuildDir}",
                 "-logFile", "unity-exportPackage.log",
                 "-exportPackage",
                 "Assets/PilgrimUnitySDK/Api",
                 "Assets/PilgrimUnitySDK/Editor",
                 "Assets/PilgrimUnitySDK/Platforms",
                 "Assets/PilgrimUnitySDK/Plugins",
                 "Assets/PilgrimUnitySDK/Types",
                 "${exportPath}",
                 "-quit"
        }
    }
}

task createTempDirectory(type: Copy) {
    from {"${pluginSource}"}
    into {"${pluginBuildDir}"}
}

task deleteTempDirectory(type: Delete) {
    delete {"${tempDir}"}
}

exportUnityPackage.dependsOn(createTempDirectory, 
                             copyPilgrimAndroid, copyAndroidWrapperLibrary, 
                             copyPilgrimiOS, loadLicenseFile)
                             
exportUnityPackage.finalizedBy(deleteTempDirectory)

defaultTasks "exportUnityPackage"