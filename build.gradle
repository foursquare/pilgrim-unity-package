plugins {
    id "de.undercouch.download" version "3.4.3"
    id "java-library"
}

project.ext {
    if (System.getenv("UNITY_ROOT") != null) {
        unity_root = System.getenv("UNITY_ROOT")
    } else {
        throw new GradleException("Set UNITY_ROOT env variable")
    }

    if (System.getenv("UNITY_SERIAL") != null) {
        unity_serial = System.getenv("UNITY_SERIAL")
    } else {
        throw new GradleException("Set UNITY_SERIAL env variable")
    }

    if (System.getenv("UNITY_USERNAME") != null) {
        unity_username = System.getenv("UNITY_USERNAME")
    } else {
        throw new GradleException("Set UNITY_USERNAME env variable")
    }

    if (System.getenv("UNITY_PASSWORD") != null) {
        unity_password = System.getenv("UNITY_PASSWORD")
    } else {
        throw new GradleException("Set UNITY_PASSWORD env variable")
    }

    pilgrimiOSVersion = "2.1.2"
    pilgrimAndroidVersion = "2.1.2"
    tempDir = file("temp").absolutePath
    pluginSource = file("source/pilgrim-unity-sdk").absolutePath
    pluginBuildDir = file("temp/pilgrim-unity-sdk").absolutePath
    exportPath = file("pilgrim-unity-sdk.unitypackage").absolutePath
}

configurations {
    pilgrim {
        repositories {
            mavenCentral()
            maven {
                url "https://maven.google.com"
            }
            maven {
                url 'https://foursquare.jfrog.io/foursquare/libs-release/'
            }
        }
    }
}

dependencies {
    pilgrim "com.foursquare:pilgrimsdk:${pilgrimAndroidVersion}"
}

// TOOD consistent naming convention for variables, camel case or underscore

task downloadPilgrimAndroid(type: Copy) {
    from(configurations.pilgrim)
    into("${tempDir}/android-deps")
}

task copyPilgrimAndroid(type: Copy) {
    from("${tempDir}/android-deps")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/Android")
}

copyPilgrimAndroid.dependsOn(downloadPilgrimAndroid)

task buildAndCleanAndroidWrapperLibrary(type: GradleBuild) {
    buildFile = "source/android-source/build.gradle"
    tasks = ["clean", "assembleRelease"]
    startParameter.projectProperties = [unity_root: "${unity_root}"]
}

task copyAndroidWrapperLibrary(type: Copy) {
    from("source/android-source/pilgrimunitysdk/build/outputs/aar")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/Android")
    include("pilgrimunitysdk-release.aar")
    rename("pilgrimunitysdk-release.aar", "pilgrimunitysdk.aar")
}

copyAndroidWrapperLibrary.dependsOn(buildAndCleanAndroidWrapperLibrary)

task downloadPilgrimiOS(type: Download) {
    src "https://foursquare.jfrog.io/foursquare/pilgrimsdk-ios/v${pilgrimiOSVersion}/Pilgrim.framework.zip"
    dest "${tempDir}/Pilgrim.framework.zip"
}

task unzipPilgrimiOS() {
    doLast {
        exec {
            executable "unzip"
            args "${tempDir}/Pilgrim.framework.zip", "-d", "${tempDir}"
        }
    }
}

task copyPilgrimiOS(type: Copy) {
    from("${tempDir}/Pilgrim-${pilgrimiOSVersion}/Pilgrim.framework")
    into("${pluginBuildDir}/Assets/PilgrimUnitySDK/Plugins/iOS/Pilgrim.framework")
    include("**")
}

copyPilgrimiOS.dependsOn(downloadPilgrimiOS, unzipPilgrimiOS)

task exportUnityPackage() {
    doLast {
        exec {
            executable "${unity_root}/Unity.app/Contents/MacOS/Unity"
            args "-gvh_disable",
                 "-batchmode",
                 "-serial",
                 "${unity_serial}",
                 "-username",
                 "${unity_username}",
                 "-password",
                 "${unity_password}",
                 "-projectPath", "${pluginBuildDir}",
                 "-logFile", "unity-exportPackage.log",
                 "-exportPackage",
                 "Assets/PilgrimUnitySDK/Api",
                 "Assets/PilgrimUnitySDK/Editor",
                 "Assets/PilgrimUnitySDK/Platforms",
                 "Assets/PilgrimUnitySDK/Plugins",
                 "Assets/PilgrimUnitySDK/Types",
                 "${exportPath}",
                 "-quit"
        }
    }
}

task returnLicense() {
    doLast {
        exec {
           executable "${unity_root}/Unity.app/Contents/MacOS/Unity"
           args "-logFile", "unity-returnlicense.log",
                "-quit",
                "-batchmode",
                "-returnlicense"
        }
    }
}

task createTempDirectory(type: Copy) {
    from {"${pluginSource}"}
    into {"${pluginBuildDir}"}
}

task deleteTempDirectory(type: Delete) {
    delete {"${tempDir}"}
}

exportUnityPackage.dependsOn(createTempDirectory, copyPilgrimAndroid, copyAndroidWrapperLibrary, copyPilgrimiOS)
exportUnityPackage.finalizedBy(deleteTempDirectory, returnLicense)

defaultTasks "exportUnityPackage"